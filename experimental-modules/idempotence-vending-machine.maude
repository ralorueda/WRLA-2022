--- We can modify the equational theory of the vending machine
--- to add some equations that express idempotence for the dollar,
--- apple and coffee symbols.
--- Note that idempotence is not specified for quarters, but only for dollars, apples
--- and cups of coffee. This is because there is already an equation that reduces the
--- repetition of four quarters to a dollar, so that adding idempotence for quarters
--- would create a conflict.

mod IDEMPOTENCE-VENDING-MACHINE is
   sorts Coin Item Marking Money State .
   subsort Coin < Money .
   subsort Money Item < Marking .

   op empty : -> Money .
   op __ : Money Money -> Money [assoc comm id: empty] .
   op __ : Marking Marking -> Marking [assoc comm id: empty] .
   op <_> : Marking -> State .
   ops $ q : -> Coin .
   ops c a : -> Item .

   var M : Marking .

   rl [buy-c] : < M $ > => < M c > [narrowing] .
   rl [buy-a] : < M $ > => < M a q > [narrowing] .
   eq [change] : q q q q M = $ M [variant] .
   eq [idem-dollar] : $ $ M = $ M [variant] .
   eq [idem-item-a] : a a M = a M [variant] .
   eq [idem-item-c] : c c M = c M [variant] .
endm
