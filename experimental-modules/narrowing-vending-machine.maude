--- This Maude's system module is a classic in the Maude community. It is the
--- coffee and apple vending machine, in which dollars and quarters are inserted
--- to buy combinations of those products. To do this, we specify that each coffee
--- costs one dollar and each apple three-quarters of a dollar. Two rules handle state
--- transitions for those specifications. Furthermore, an equation is used to specify
--- the change of four-quarters of a dollar to one dollar. Note the addition of a
--- variable M of type Marking to make the rules and equations ACU-coherent.

mod NARROWING-VENDING-MACHINE is
   sorts Coin Item Marking Money State .
   subsort Coin < Money .
   op empty : -> Money .
   op __ : Money Money -> Money [assoc comm id: empty] .
   subsort Money Item < Marking .
   op __ : Marking Marking -> Marking [assoc comm id: empty] .
   op <_> : Marking -> State .
   ops $ q : -> Coin .
   ops c a : -> Item .
   var M : Marking .
   rl [buy-c] : < M $ > => < M c > [narrowing] .
   rl [buy-a] : < M $ > => < M a q > [narrowing] .
   eq [change] : q q q q M = $ M [variant] .
endm